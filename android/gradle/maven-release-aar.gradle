apply plugin: 'maven-publish'

//打包main目录下代码和资源的 task
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}
def GROUP_ID = "com.microsoft.quickauth"
def ARTIFACT_ID = "signin"
def VERSION = "0.0.8"
//def MAVEN_GROUP_URL = "https://microsoft.pkgs.visualstudio.com/Edge/_packaging/EdgeMobileWebView/maven/v1"
ext["repoUsername"] = ''
ext["repoUserToken"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    println "Found secret props file, loading props"
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    println "No props file, loading env vars"
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = GROUP_ID
                artifactId = ARTIFACT_ID
                version = VERSION
            }
            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = GROUP_ID
                artifactId = ARTIFACT_ID
                version = VERSION
            }
        }
        repositories {
            maven {
                url 'https://microsoft.pkgs.visualstudio.com/Edge/_packaging/QuickAuth/maven/v1'
                name 'QuickAuth'
                authentication {
                    basic(BasicAuthentication)
                }
                credentials {
                    username repoUsername
                    password repoUserToken
                }
            }
        }
    }
}

